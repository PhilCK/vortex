
/*@
	## Description
	
	Logic for the Enemy Splitter.


	## Params

	

	## Events


	
*/


// ** Splitter States ** //
enum EnemySplitterStates { MOVING, SPLITTING, };	



class EnemySplitterController : GameComponent
{
	// ** Engine Hooks ** //

	private void onParams(const Param &in params) override
	{

	}



	private void onStart() override
	{

	}



	private void onThink(const float dt) override
	{
		switch(m_state)
		{
			case(EnemySplitterStates::MOVING):
			{
				getVortexTransform().setDepth(getVortexTransform().getDepth() + (dt * 2.f));

				if(getVortexTransform().getDepth() > 2.f)
				{
					m_state = EnemySplitterStates::SPLITTING;
				}
				break;
			}

			case(EnemySplitterStates::SPLITTING):
			{
				m_cellDivideTimer += dt;

				if(m_cellDivideTimer > m_cellDivideTimeParam)
				{
					getCellDivide().tryDivide();

					m_cellDivideTimer = 0;
				}
				break;
			}
		} // switch(...)
	}



	private bool onEvent(const Event &in data) override
	{
		// Was this entity created via a cell divide?
		{
			const uint cellDivideEventID = getOwner().getEventManager().getEventID("Direct-CellDivided");

			if(data.getID() == cellDivideEventID)
			{
				m_state = EnemySplitterStates::SPLITTING;

				VortexSpawnPosition @spawnPos = getOwner().getComponent("VortexSpawnPosition");
				spawnPos.setSpawned();

				return false;
			}
		}

		return false;
	}



	// ** Comp Helpers ** //

	private VortexTransform@ getVortexTransform()
	{
		if(m_vortexTransform is null)
		{
			@m_vortexTransform = @getOwner().getComponent("VortexTransform");

			if(m_vortexTransform is null)
			{
				Print("EnemySplitterController: Failed to find vortex-transform.");
				return null;
			}
		}

		return m_vortexTransform;
	}



	private CellDivide@ getCellDivide()
	{
		if(m_cellDivide is null)
		{
			@m_cellDivide = @getOwner().getComponent("CellDivide");

			if(m_cellDivide is null)
			{
				Print("EnemySplitterController: Failed to find CellDivide.");
				return null;
			}
		}

		return m_cellDivide;
	}	



	// ** Member Vars ** //

	private float 						m_cellDivideTimeParam	= 3.f;

	private float 						m_cellDivideTimer   	= 0.f;
	private CellDivide 					@m_cellDivide 			= null;
	private VortexTransform 			@m_vortexTransform  	= null;
	private EnemySplitterStates			m_state 				= EnemySplitterStates::MOVING;
};
